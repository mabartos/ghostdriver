/*
This file is part of the GhostDriver by Ivan De Marino <http://ivandemarino.me>.

Copyright (c) 2012-2014, Ivan De Marino <http://ivandemarino.me>
All rights reserved.

Redistribution and use in source and binary forms, with or without modification,
are permitted provided that the following conditions are met:

    * Redistributions of source code must retain the above copyright notice,
      this list of conditions and the following disclaimer.
    * Redistributions in binary form must reproduce the above copyright notice,
      this list of conditions and the following disclaimer in the documentation
      and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR
ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
(INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON
ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
(INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

apply plugin: "java"
apply plugin: "idea"
apply plugin: "eclipse"

defaultTasks 'check', 'test'

task wrapper(type: Wrapper) {
    gradleVersion = "4.4.1"
    jarFile = "gradle/gradle-wrapper.jar"
}

repositories {
    mavenCentral()
}

dependencies {
    // Test Dependencies
    ["selenium-java", "selenium-remote-driver"].each {
        testCompile "org.seleniumhq.selenium:$it:3.8.1"
    }
    testCompile "com.google.code.findbugs:jsr305:3.0.1"
    testCompile "junit:junit:4.12"
    testCompile "org.mortbay.jetty:jetty:6.1.26"
    testCompile "commons-fileupload:commons-fileupload:1.3.3"
    testCompile "commons-io:commons-io:2.6"
    testCompile "com.github.detro:browsermob-proxy-client:0.1.3"

    // Library under test (PhantomJS Driver, aka "PhantomJS WebDriver Java Binding")
    testCompile files("../../binding/java/jars/phantomjsdriver-1.4.4.jar")
}

tasks.withType(JavaExec) {
    classpath = configurations.compile + sourceSets.test.output
    args project.hasProperty("args") ? project.args.split("\\s") : []
}

// copy all dependencies to build/lib for easier IDE setup without need for Gradle IDE plugin
task libs(type: Sync) {
  from configurations.testCompile
  into "$buildDir/lib"
}

compileJava.dependsOn libs

test {
    doFirst {
      new File('phantomjsdriver.log').delete()
    }

    testLogging.showStandardStreams = true

    beforeTest { descriptor ->
        logger.lifecycle("start " + descriptor + " in " + descriptor.getParent())
    }
    afterTest { descriptor, result ->
        logger.lifecycle(result.toString() + " for " + descriptor + " in " + descriptor.getParent())
    }
}
